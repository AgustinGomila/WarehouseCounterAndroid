apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 32
    //// Esto sirve la actualizar el número de versión con cada compilación
    def versionPropsFile = file('version.properties')
    Properties versionProps = new Properties()
    if (!versionPropsFile.exists()) {
        versionProps['VERSION_PATCH'] = "0"
        versionProps['VERSION_NUMBER'] = "0"
        versionProps['VERSION_BUILD'] = "-1"
        // I set it to minus one so the first build is 0 which isn't super important.
        versionProps.store(versionPropsFile.newWriter(), null)
    }
    if (versionPropsFile.canRead()) {
        versionProps.load(new FileInputStream(versionPropsFile))
        def value = 0

        def runTasks = gradle.startParameter.taskNames
        if (':warehouseCounter:assembleRelease' in runTasks ||
                ':warehouseCounter:bundleRelease' in runTasks) {
            value = 1
        }

        def versionMajor = 12
        def versionMinor = 0
        def versionPatch = versionProps['VERSION_PATCH'].toInteger() + value
        def versionBuild = versionProps['VERSION_BUILD'].toInteger() + 1
        def versionNumber = versionProps['VERSION_NUMBER'].toInteger() + value

        versionProps['VERSION_PATCH'] = versionPatch.toString()
        versionProps['VERSION_BUILD'] = versionBuild.toString()
        versionProps['VERSION_NUMBER'] = versionNumber.toString()

        versionProps.store(versionPropsFile.newWriter(), null)

        defaultConfig {
            applicationId "com.dacosys.warehouseCounterM12"
            minSdkVersion 23
            targetSdkVersion 32

            versionCode versionNumber
            versionName "${versionMajor}.${versionMinor}.${versionPatch} (${versionBuild})"
            testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        }
    } else {
        throw new GradleException("Could not read version.properties!")
    }
    buildTypes {
        debug {
            initWith debug
            debuggable true
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile(
                    'proguard-android.txt'),
                    'proguard-rules.pro'
            matchingFallbacks = ['debug']
        }
        release {
            // Enables code shrinking, obfuscation, and optimization for only
            // your project's release build type.
            minifyEnabled true

            // Enables resource shrinking, which is performed by the
            // Android Gradle plugin.
            shrinkResources true

            // Includes the default ProGuard rules files that are packaged with
            // the Android Gradle plugin. To learn more, go to the section about
            // R8 configuration files.
            proguardFiles getDefaultProguardFile(
                    'proguard-android.txt'),
                    'proguard-rules.pro'
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }
    buildFeatures {
        dataBinding true
        viewBinding true
    }
    productFlavors {
    }
    buildToolsVersion = '30.0.3'
    kotlinOptions {
        jvmTarget = '1.8'
    }
    defaultConfig {
        proguardFiles
        manifestPlaceholders = [:]
        targetSdkVersion 32
        minSdkVersion 23
    }
    lint {
        abortOnError false
        checkReleaseBuilds false
        disable 'MissingTranslation'
    }
    namespace 'com.dacosys.warehouseCounter'
}

dependencies {
    // Permite utilizar API restringidas no públicas del SDK
    implementation 'com.github.tiann:FreeReflection:3.1.0'

    // Permite utilizar las librerías Zxing en API 23
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.6'

    // debugImplementation because LeakCanary should only run in debug builds.
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.9.1'

    // TODO: Implementar Unit Test
    //testImplementation 'junit:junit:4.13.2'
    //testImplementation 'com.google.truth:truth:1.1.3'

    //androidTestImplementation 'androidx.test:runner:1.4.0'
    //androidTestImplementation('androidx.test.espresso:espresso-core:3.4.0', {
    //    exclude group: 'com.android.support', module: 'support-annotations'
    //})

    // IMAGECONTROL Library
    implementation project(path: ':imageControl')

    // Constaint Layout
    // No actualizar la 2.0.1 (Fallos en: 2.0.2, 2.0.3, 2.0.4, 2.1.0),
    // Todas tienen un bug al finalizar una animación que no re-dibuja los fragmentos
    implementation 'androidx.constraintlayout:constraintlayout:2.0.1'

    // Common status codes that are often shared across API surfaces
    implementation 'com.google.android.gms:play-services-vision:20.1.3'

    // JSON Library
    implementation 'com.google.code.gson:gson:2.9.0'

    // HTTP library
    implementation 'com.android.volley:volley:1.2.1'

    // SOAP Library
    implementation 'com.google.code.ksoap2-android:ksoap2-android:3.6.2'

    // KEYBOARD Visibility library
    implementation 'net.yslibrary.keyboardvisibilityevent:keyboardvisibilityevent:2.3.0'

    // Apache Commons Net
    implementation 'commons-net:commons-net:3.8.0'

    // PARCELER
    implementation group: 'org.parceler', name: 'parceler-api', version: '1.1.13'

    // Android Asynchronous Http Client
    implementation 'com.loopj.android:android-async-http:1.4.11'

    //coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4'

    // ImageControl
    implementation project(':imageControl')

    // Floating Camera Window
    implementation project(':easyfloat')

    // Jotter (Activity & fragment lifecycle listener)
    implementation project(':jotter')

    // Barcode Library
    implementation project(':zxing-android-embedded')

    // Use the SplashScreen compat library.
    implementation 'androidx.core:core-splashscreen:1.0.0-rc01'
    implementation 'androidx.core:core-ktx:1.9.0-alpha05'
    implementation 'androidx.preference:preference-ktx:1.2.0'

    // Some AndroidX Libs
    implementation 'androidx.annotation:annotation:1.4.0'
    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'androidx.compose.animation:animation-core:1.3.0-alpha01'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.percentlayout:percentlayout:1.0.0'
    implementation 'com.google.android.material:material:1.6.1'

    // SwipeR
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"

    // Para ActivityResults
    implementation 'androidx.activity:activity-ktx:1.6.0-alpha05'
    implementation 'androidx.fragment:fragment-ktx:1.5.0'

    // HONEYWELL NATIVE DRIVER
    implementation files('libs/honeywell/DataCollection.aar')
}
repositories {
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/ksoap2-android-releases/' }
}
